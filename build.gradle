import net.darkhax.curseforgegradle.TaskPublishCurseForge

buildscript {
    repositories { mavenCentral() }

    dependencies {
        classpath("io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0")
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id "com.diffplug.spotless" version "7.0.0.BETA1"
    id 'net.neoforged.gradle.userdev' version '7.0.145'
    id 'net.darkhax.curseforgegradle' version '1.1.24'
}

apply plugin: 'io.codearte.nexus-staging'

ext {
    dev_id = 'yusuf'
    dev_name = 'Yusuf Ismail'
    dev_email = 'ysufgamer222@gmail.com'
    dev_organization = 'Real Yusuf Ismail'
    dev_organizationUrl = 'https://github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo'

    gpl_name = 'Apache License 2.0'
    gpl_url = 'https://github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo/LICENSE.txt'

    // Make sure we have a default for initial configuration evaluation
    isReleaseVersion = false
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

project.version = mod_version
project.group = 'io.github.realyusufismail'

// Mod name
base.archivesName.set('realyusufismailcore-neoforge')

final var modId = mod_id
final var mcVersion = minecraft_version
final var projectId = project_id

def releaseVersion = !project.version.endsWith("SNAPSHOT")


// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', modId
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', modId
        programArgument '--nogui'
    }

    gameTestServer {

        systemProperty 'forge.enabledGameTestNamespaces', modId
    }

    data {
        workingDirectory project.file('run-data')

        programArguments.addAll '--mod', modId, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenCentral()
}

dependencies {
    implementation("net.neoforged:neoforge:${neoVersion}")
    //Logger
    implementation("ch.qos.logback:logback-classic:${logbackVersion}")
    implementation("ch.qos.logback:logback-core:${logbackVersion}")
    //lombok
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    //test
    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
}

test {
    useJUnitPlatform()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neoVersion,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

afterEvaluate { project ->
    ext.isReleaseVersion = !version.endsWith("RELEASE")
}

compileJava {
    // Makes spotlessApply task run on every compile/build.
    dependsOn 'spotlessApply'

    // Nails the Java-Version of every Subproject
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

spotless {
    java {
        // Excludes build folder since it contains generated java classes.
        targetExclude("build/**")
        palantirJavaFormat()
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
        licenseHeader(
                """/*
 * Copyright 2024 RealYusufIsmail.
 *
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */ """)
    }
}

javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    def isReleaseVersion = !version.toString().endsWith("SNAPSHOT")
    publications {
        mavenJava(MavenPublication) {
            //artifactId = project.artifactId // or maybe archiveBaseName?
            from components.java
            pom {
                name = project.archivesBaseName
                description = mod_description
                url = "https://github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo"
                packaging = "jar"

                licenses {
                    license {
                        name = "${gpl_name}"
                        url = "${gpl_url}"
                    }
                }
                developers {
                    developer {
                        id = "${dev_id}"
                        name = "${dev_name}"
                        email = "${dev_email}"
                        organization = "${dev_organization}"
                        organizationUrl = "${dev_organizationUrl}"
                    }
                }
                scm {
                    connection = 'https://github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo.git'
                    developerConnection = 'scm:git:ssh://git@github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo.git'
                    url = 'github.com/RealYusufIsmail-Mc-Mods/RealYusufIsmail-Core-Neo'
                }
            }
        }
    }
    repositories {
        maven {
            name = "MavenCentral"
                def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = (isReleaseVersion) ? releaseRepo : snapshotRepo

                credentials {
                    username = project.hasProperty('MAVEN_USERNAME') ? ossrhUsername : "Unknown user"
                    password = project.hasProperty('MAVEN_PASSWORD') ? ossrhPassword : "Unknown password"
                }
            }
    }
}

//For future reference if issue occurs with signing just put secret-keys.gpg locally and update main gradle.properties accordingly.
signing {
    afterEvaluate { project ->
        def isReleaseVersion = releaseVersion && gradle.taskGraph.hasTask("publish")
        setRequired(isReleaseVersion)
        sign(publishing.publications.mavenJava)
    }
}

nexusStaging {
    packageGroup = 'io.github.realyusufismail'
    serverUrl = "https://s01.oss.sonatype.org/service/local"
    username = project.hasProperty('MAVEN_USERNAME') ? project.property('MAVEN_USERNAME') : 'username'
    password = project.hasProperty('MAVEN_PASSWORD') ? project.property('MAVEN_PASSWORD') : 'password'
}


tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group = "CurseForge"
    dependsOn("build")
    disableVersionDetection()

    var token = project.hasProperty('curseforge.token') ? project.property('curseforge.token') as String : ""
    apiToken = token

    final var jar = file("build/libs/${base.archivesName.get()}-${project.version}.jar")

    final var mainFile = upload(projectId, jar)
    mainFile.changelog = file("CHANGELOG.md").getText()
    mainFile.changelogType = "markdown"
    mainFile.releaseType = "release"
    mainFile.addJavaVersion("Java 21")
    mainFile.addModLoader("NeoForge")
    mainFile.addGameVersion(mcVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}